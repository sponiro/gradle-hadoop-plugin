buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'bintray'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'com.google.guava', name: 'guava', version: '15.0'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.hamcrest', name: 'hamcrest-integration', version: '1.3'
}

group = 'de.fanero.gradle.plugin.hadoop'
version = '0.2'
def compatibility = 1.5
sourceCompatibility = compatibility
targetCompatibility = compatibility

task sourcesJar(type: Jar) {
    description = 'Creates sources JAR'
    classifier = 'sources'

    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar

            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'Gradle Hadoop plugin')
                root.appendNode('description', 'Gradle plugin supporting development of MapReduce applications with Hadoop.')
                root.appendNode('url', 'https://github.com/sponiro/gradle-hadoop-plugin')
                root.appendNode('inceptionYear', '2013')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/sponiro/gradle-hadoop-plugin')
                scm.appendNode('connection', 'scm:https://github.com/sponiro/gradle-hadoop-plugin.git')
                scm.appendNode('developerConnection', 'scm:git:https://github.com/sponiro/gradle-hadoop-plugin.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def rkuehne = developers.appendNode('developer')
                rkuehne.appendNode('id', 'rkuehne')
                rkuehne.appendNode('name', 'Robert KÃ¼hne')
                rkuehne.appendNode('email', 'sponiro@gmail.com')
            }
        }
    }
}

bintrayUpload {
    dependsOn "publishMavenJavaPublicationToMavenLocal"
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : null
    key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : null
    publications = ['mavenJava']
    pkg {
        repo = 'gradle-plugins'
        name = 'gradle-hadoop-plugin'
        licenses = ['Apache-2.0']
    }
}